<!DOCTYPE html>
<html lang="en">
<head>
  <title>Interstellar - Interactive wormhole &amp; black hole</title>
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
  <style>
    body {
      background:#000;
      color: #eee;
      padding:0;
      margin:0;
      font-weight:bold;
      overflow:hidden;

      font-family: Monospace;
      font-size: 12px;
    }

    #info {
      position: fixed;
      top: 0;
      width: 100%;
      padding: 5px;
      text-align: center;
      z-index: 10;
    }

    #resolution {
      position: fixed;
      bottom: 5px;
      right: 5px;
      z-index: 10;
    }

    #resolution > label {
      display: block;
    }

    h3 {
      margin: 0;
    }

    a {
      color: #0080ff;
    }

    kbd { color: orange }

    #container {
      position: fixed;
      left: 0;
      right: 0;
      top: 0;
      bottom: 0;
    }

    #keyboard-controls { display: block; }
    #tablet-controls { display: none; }
    .tablet #keyboard-controls { display: none; }
    .tablet #tablet-controls { display: block; }

    canvas { display: block; }
  </style>
</head>

<body>

<div id="info">
  <h3>Controls:</h3>
  <div id="keyboard-controls">
    <kbd>WASD</kbd> move,
    <kbd>R|F</kbd> up | down,
    <kbd>Q|E</kbd> roll,
    <kbd>up|down</kbd> pitch,
    <kbd>left|right</kbd> yaw
    <kbd>shift</kbd> speed up<br>
    <kbd>space</kbd> toggle drag controls
  </div>
  <div id="tablet-controls">
    Point your device around you to look around. Touch the screen to move forwards.
  </div>
</div>

<div id="resolution">
  <h3>Pixel size</h3>
  <label><input type="radio" name="resolution" value="4" checked> 4x4</label>
  <label><input type="radio" name="resolution" value="2"> 2x2</label>
  <label><input type="radio" name="resolution" value="1"> 1x1</label>
</div>

<div id="container"></div>

<script type="x-shader/x-fragment" id="fragmentShader">
#define ID_SATURN 0
#define ID_SATURN_RING 1
#define ID_GALAXY1 2
#define ID_GALAXY2 3
#define ID_BLACKHOLE 4
#define ID_BLACKHOLE_DISK 5
#define ID_PLANET 6

varying vec4 rayDir;

uniform vec3 lightDirection;

uniform vec3 planetDiffuse;
uniform vec3 planetSpecular;

uniform vec4 wormhole;
uniform vec4 blackhole;

uniform vec4 saturn;
uniform vec4 planet;

uniform vec4 blackholeDisk;
uniform vec4 saturnRings;

uniform sampler2D texSaturn;
uniform sampler2D texSaturnRings;
uniform sampler2D texGalaxy1;
uniform sampler2D texGalaxy2;
uniform sampler2D texAccretionDisk;

uniform float worldSize;
uniform int startGalaxy;

const float lightSpeed = 0.2;

const float INFINITY = 1000000.0;
const float GALAXY_EDGE = 10000.0;

const float EPSILON = 0.0001;
const float r2 = 0.0625;

const float PI = 3.14159265359;
const float TWOPI = 6.28318530718;

float gravityWormhole = wormhole.w * lightSpeed * lightSpeed;
float gravityBlackhole = blackhole.w * lightSpeed * lightSpeed;

vec3 saturnColor(vec3 pos)
{
  vec2 uv = vec2(
    0.5 + atan(pos.z, pos.x) / TWOPI,
    0.5 - asin(pos.y) / PI
  );
  return texture2D(texSaturn, uv).rgb;
}

vec3 panoramaColor(float n, vec3 pos)
{
  vec2 uv = vec2(
    0.5 - atan(pos.z, pos.x) / TWOPI,
    0.5 - asin(pos.y) / PI
  );
  if (n < 0.5) return texture2D(texGalaxy1, uv).rgb;
  else return texture2D(texGalaxy2, uv).rgb;
}

vec3 accretionDiskColor(vec3 pos)
{
  pos = pos - blackhole.xyz;
  float dist = length(pos);

  float r1 = length(blackholeDisk.xyz);
  float r2 = blackholeDisk.w;

  // Important! Scale radii according to black hole
  float v = clamp((dist - r1) / (r2 - r1), 0.0, 1.0);

  // Normalize all relevant vectors for computing U
  vec3 ringNormal = blackholeDisk.xyz / r1;
  pos /= dist;

  // Construct a quaternion that rotates the ring and pos onto the xy-plane
  vec3 origin = vec3(0.0, 0.0, 1.0);
  vec4 axisAngle = vec4(cross(ringNormal, origin), dot(ringNormal, origin));
  vec4 q = vec4(sin(axisAngle.w * 0.5) * axisAngle.xyz, cos(axisAngle.w * 0.5));

  vec4 i = vec4(
    + q.w * pos.x + q.y * pos.z - q.z * pos.y,
    + q.w * pos.y + q.z * pos.x - q.x * pos.z,
    + q.w * pos.z + q.x * pos.y - q.y * pos.x,
    - q.x * pos.x - q.y * pos.y - q.z * pos.z
  );

  pos.x = i.x * q.w - i.w * q.x - i.y * q.z + i.z * q.y;
  pos.y = i.y * q.w - i.w * q.y - i.z * q.x + i.x * q.z;

  float angle = atan(pos.y, pos.x);
  float u = 0.5 - angle / TWOPI;
  return texture2D(texAccretionDisk, vec2(u, v)).rgb;
}

float sphereDistance(vec3 rayPosition, vec3 rayDirection, vec4 sphere)
{
  vec3 v;
  float p, d;
  v = rayPosition - sphere.xyz;
  p = dot(rayDirection, v);
  d = p * p + sphere.w * sphere.w - dot(v, v);

  return d < 0.0 ? -1.0 : -p - sqrt(d);
}

vec3 saturnRingColor(vec3 pos)
{
  pos = pos - saturn.xyz;

  float r1 = length(saturnRings.xyz);
  float r2 = saturnRings.w;

  // Important! Scale radii according to saturn
  float v = clamp((length(pos) - r1) / (r2 - r1), 0.0, 1.0);

  vec3 color = texture2D(texSaturnRings, vec2(0.5, v)).rgb;

  float objectDistance = sphereDistance(saturn.xyz + pos, lightDirection, saturn);
  if (objectDistance > 0.0)
  {
    return color * 0.1;
  }
  return color;
}

float ringDistance(vec3 rayPosition, vec3 rayDirection, vec3 center, vec4 definition)
{
  float r1 = length(definition.xyz);
  float r2 = definition.w;
  vec3 normal = definition.xyz / r1;

  float denominator = dot(rayDirection, normal);
  float constant = -dot(center, normal);
  float distanceToCenter;
  if (abs(denominator) < EPSILON)
  {
    return -1.0;
  }
  else
  {
    float t = -(dot(rayPosition, normal) + constant) / denominator;
    if (t < 0.0) return -1.0;

    vec3 intersection = rayPosition + t * rayDirection;
    distanceToCenter = length(intersection - center);
    if (distanceToCenter >= r1 && distanceToCenter <= r2)
    {
      return t;
    }
    return -1.0;
  }
}

vec3 computeShading(vec3 light, vec3 view, vec3 normal, vec3 diffuse, vec3 specular, vec3 ambient)
{
  float lambertian = max(dot(light, normal), 0.0);
  vec3 reflectDir = reflect(-light, normal);
  float specAngle = max(dot(reflectDir, view), 0.0);
  float specularAmount = pow(specAngle, 4.0);
  return ambient + lambertian * diffuse + specularAmount * specular;
}

void testDistance(int i, float distance, inout float currentDistance, inout int currentObject)
{
  if (distance >= EPSILON && distance < currentDistance)
  {
    currentDistance = distance;
    currentObject = i;
  }
}

vec3 raytrace(vec3 rayPosition, vec3 rayDirection)
{
  float currentDistance = INFINITY;
  int   currentObject = -1, prevObject = -1;
  float   currentGalaxy = float(startGalaxy);
  float   otherGalaxy = 1.0 - currentGalaxy;
  vec3  normal;

  float stepSize, rayDistance;
  vec3 gravityVector, rayAccel;
  float objectDistance;

  vec3 color = vec3(0.0);

  for (int i = 0; i < 100; i++)
  {
    currentDistance = INFINITY;

    // Bend the light towards the wormhole
    gravityVector = wormhole.xyz - rayPosition;
    rayDistance = length(gravityVector);

    // 0.86: rate of smaller steps when approaching wormhole
    stepSize = rayDistance - wormhole.w * 0.86;

    // rayAccel = normalize(gravityVector) * gravityWormhole / (rayDistance * rayDistance);
    rayAccel = gravityVector * gravityWormhole / (rayDistance * rayDistance * rayDistance);

    if (currentGalaxy > 0.5)
    {
      // Bend the light towards the black hole
      gravityVector = blackhole.xyz - rayPosition;
      rayDistance = length(gravityVector);

      // 0.05: rate of smaller steps when approaching blackhole
      stepSize = min(stepSize, rayDistance - blackhole.w * 0.05);

      // rayAccel += normalize(gravityVector) * gravityBlackhole / (rayDistance * rayDistance)
      rayAccel += gravityVector * gravityBlackhole / (rayDistance * rayDistance * rayDistance);
    }

    rayDirection = normalize(rayDirection * lightSpeed + rayAccel * stepSize);

    if (stepSize <= 0.005)
    {
      currentObject = -1;
      break;
    }

    if (currentGalaxy < 0.5)
    {
      objectDistance = sphereDistance(rayPosition, rayDirection, saturn);
      testDistance(ID_SATURN, objectDistance, currentDistance, currentObject);

      objectDistance = ringDistance(rayPosition, rayDirection, saturn.xyz, saturnRings);
      testDistance(ID_SATURN_RING, objectDistance, currentDistance, currentObject);

      testDistance(ID_GALAXY1, GALAXY_EDGE, currentDistance, currentObject);
    }
    else
    {
      objectDistance = sphereDistance(rayPosition, rayDirection, planet);
      testDistance(ID_PLANET, objectDistance, currentDistance, currentObject);

      // Test against a bit smaller sphere due to precision errors
      objectDistance = sphereDistance(rayPosition, rayDirection, vec4(blackhole.xyz, blackhole.w * 0.93));
      testDistance(ID_BLACKHOLE, objectDistance, currentDistance, currentObject);

      objectDistance = ringDistance(rayPosition, rayDirection, blackhole.xyz, blackholeDisk);
      testDistance(ID_BLACKHOLE_DISK, objectDistance, currentDistance, currentObject);

      testDistance(ID_GALAXY2, GALAXY_EDGE, currentDistance, currentObject);
    }

    rayDistance = lightSpeed * stepSize;

    // Check if we hit any object, and if so, stop integrating
    if (currentObject != -1 && currentDistance <= rayDistance)
    {
      // But if it's something transparent, get its color, and continue
      if (currentObject == ID_BLACKHOLE_DISK)
      {
        vec3 targetPosition = rayPosition + rayDirection * currentDistance;
        color += accretionDiskColor(targetPosition);
        currentObject = -1;
        prevObject = ID_BLACKHOLE_DISK;
      }
      else if (currentObject == ID_SATURN_RING)
      {
        vec3 targetPosition = rayPosition + rayDirection * currentDistance;
        if (prevObject != ID_SATURN_RING)
        {
          color += saturnRingColor(targetPosition);
        }
        currentObject = -1;
        prevObject = ID_SATURN_RING;
      }
      else
      {
        break;
      }
    }

    float d = sphereDistance(rayPosition, rayDirection, wormhole);
    if (d > 0.0 && d < rayDistance)
    { // Ray goes through wormhole
      currentGalaxy = otherGalaxy;
      otherGalaxy = 1.0 - currentGalaxy;
      vec3 intersection = rayPosition + rayDirection * d;
      gravityVector = normalize(intersection - wormhole.xyz);

      rayPosition = 2.0 * wormhole.xyz - intersection;
      rayDirection = -reflect(rayDirection, gravityVector);
    }
    else
    {
      rayPosition += rayDirection * rayDistance;
    }
  }

  vec3 currentPosition = rayPosition + rayDirection * currentDistance;

  if (currentObject == ID_GALAXY1 || currentObject == ID_GALAXY2)
  {
    color += panoramaColor(currentGalaxy, rayDirection);
  }
  else if (currentObject == ID_SATURN)
  {
    normal = (currentPosition - saturn.xyz) / saturn.w;

    vec3 diffuse = saturnColor(normal);
    vec3 specular = vec3(0.0);
    vec3 ambient = vec3(0.0);

    float objectDistance = ringDistance(currentPosition, lightDirection, saturn.xyz, saturnRings);
    if (objectDistance > 0.0)
    {
      vec3 ringColor = saturnRingColor(currentPosition + lightDirection * objectDistance);
      diffuse *= 1.0 - 0.333 * ringColor.r - 0.333 * ringColor.g - 0.334 * ringColor.b;
    }

    color += computeShading(lightDirection, -rayDirection, normal, diffuse, specular, ambient);
  }
  else if (currentObject == ID_PLANET)
  {
    normal = (currentPosition - planet.xyz) / planet.w;

    // light direction for black hole-orbiting planet is towards the blackhole
    vec3 lightDirection2 = normalize(blackhole.xyz - planet.xyz);

    vec3 diffuse = planetDiffuse;
    vec3 specular = planetSpecular;
    vec3 ambient = vec3(0.0);
    color += computeShading(lightDirection2, -rayDirection, normal, diffuse, specular, ambient);
  }

  return color;
}

void main()
{
  gl_FragColor = vec4(raytrace(cameraPosition, normalize(rayDir.xyz)), 1.0);
}
</script>
<script type="x-shader/x-vertex" id="vertexShader">
uniform mat4 rayMatrix;

varying vec4 rayDir;

void main() {
  rayDir = rayMatrix * vec4(position.xy, 1.0, 0.0);

  gl_Position = vec4(position.xy, 0.0, 1.0);
}
</script>

<script src="js/three.min.js"></script>
<script src="js/dat.gui.min.js"></script>

<script src="js/ConvolutionShader.js"></script>
<script src="js/CopyShader.js"></script>
<script src="js/FilmShader.js"></script>

<script src="js/EffectComposer.js"></script>
<script src="js/ShaderPass.js"></script>
<script src="js/MaskPass.js"></script>
<script src="js/RenderPass.js"></script>
<script src="js/BloomPass.js"></script>
<script src="js/FilmPass.js"></script>

<script src="js/FlyControls.js"></script>
<script src="js/DeviceOrientationControls.js"></script>
<script src="js/TabletControls.js"></script>
<script src="js/Detector.js"></script>
<script src="js/stats.min.js"></script>
<script src="js/main.js"></script>

</body>
</html>
