<!DOCTYPE html>
<html lang="en">
<head>
  <title>Interstellar - Interactive wormhole &amp; black hole</title>
  <meta charset="utf-8">
  <meta name="viewport" content="width=device-width, user-scalable=no, minimum-scale=1.0, maximum-scale=1.0">
  <style>
    body {
      background:#000;
      color: #eee;
      padding:0;
      margin:0;
      font-weight:bold;
      overflow:hidden;

      font-family:Monospace;
      font-size:13px;
      text-align:center;
    }

    #info {
      position: absolute;
      top: 0px; width: 100%;
      padding: 5px;
    }

    a {
      color: #0080ff;
    }

    kbd { color: orange }
    
    canvas { display: block; }
  </style>
</head>

<body>

<div id="info">
  <h3>Controls:</h3>
  <kbd>WASD</kbd> move,
  <kbd>R|F</kbd> up | down,
  <kbd>Q|E</kbd> roll,
  <kbd>up|down</kbd> pitch,
  <kbd>left|right</kbd> yaw
  <kbd>shift</kbd> speed up
  <kbd>space</kbd> toggle drag controls
</div>

<script type="x-shader/x-fragment" id="fragmentShaderDepth">
#define ID_SATURN 0
#define ID_SATURN_RING 1
#define ID_GALAXY1 2
#define ID_GALAXY2 3
#define ID_BLACKHOLE 4
#define ID_BLACKHOLE_DISK 5
#define ID_PLANET 6

varying vec2 pos;

uniform vec3 lightDirection;
uniform mat4 rayMatrix;

uniform vec3 planetDiffuse;
uniform vec3 planetSpecular;

uniform vec4 wormhole;
uniform vec4 blackhole;
uniform float gravityWormhole;
uniform float gravityBlackhole;

uniform vec4 saturn;
uniform vec4 planet;

uniform sampler2D texSaturn;
uniform sampler2D texGalaxy1;
uniform sampler2D texGalaxy2;

uniform float c;

uniform float stepSize;
uniform float worldSize;
uniform int startGalaxy;

const float INFINITY = 1000000.0;
const float GALAXY_EDGE = 10000.0;

const float EPSILON = 0.0001;
const float r2 = 0.0625;

const float PI = 3.14159265359;
const float TWOPI = 6.28318530718;

vec3 saturnColor(vec3 pos)
{
  float phi, theta;
  vec2 uv;
  phi = acos(pos.y);
  theta = acos(-pos.x / sin(phi)) / TWOPI;
  uv = vec2(
    pos.z >= 0.0 ? 1.0 - theta : theta,
    phi / PI
  );
  return texture2D(texSaturn, uv).rgb;
}

vec3 panoramaColor(int n, vec3 pos)
{
  float phi, theta;
  vec2 uv;
  phi = acos(pos.y);
  theta = acos(-pos.x / sin(phi)) / TWOPI;
  uv = vec2(
    pos.z >= 0.0 ? 1.0 - theta : theta,
    phi / PI
  );
  if (n == 0) return texture2D(texGalaxy1, uv).rgb;
  else return texture2D(texGalaxy2, uv).rgb;
}

float sphereDistance(vec3 rayPosition, vec3 rayDirection, vec4 sphere)
{
  vec3 v;
  float p, d;
  v = rayPosition - sphere.xyz;
  p = dot(rayDirection, v);
  d = p * p + sphere.w * sphere.w - dot(v, v);

  return d < 0.0 ? -1.0 : -p - sqrt(d);
}

float ringDistance(vec3 rayPosition, vec3 rayDirection, vec3 center, vec3 normal, float r1, float r2)
{
  float denominator = dot(rayDirection, normal);
  float constant = -dot(center, normal);
  float distanceToCenter;
  if (abs(denominator) < EPSILON)
  {
    return -1.0;
  }
  else
  {
    float t = -(dot(rayPosition, normal) + constant) / denominator;
    if (t < 0.0) return -1.0;

    vec3 intersection = rayPosition + t * rayDirection;
    distanceToCenter = length(intersection - center);
    if (distanceToCenter >= r1 && distanceToCenter <= r2)
    {
      return t;
    }
    return -1.0;
  }
}

float panoramaDistance(vec3 rayPosition, vec3 rayDirection)
{
#if NO_EDGE
  return GALAXY_EDGE;
#else
  float p, d;
  p = dot(rayPosition, rayPosition);
  if (p >= worldSize * worldSize) return -1.0;
  d = -dot(rayPosition, rayDirection);
  return d + sqrt(worldSize * worldSize - p + d * d);
#endif
}

vec3 computeShading(vec3 light, vec3 view, vec3 normal, vec3 diffuse, vec3 specular, vec3 ambient)
{
  float lambertian = max(dot(light, normal), 0.0);
  vec3 reflectDir = reflect(-light, normal);
  float specAngle = max(dot(reflectDir, view), 0.0);
  float specularAmount = pow(specAngle, 4.0);
  return ambient + lambertian * diffuse + specularAmount * specular;
}

void testDistance(int i, float distance, inout float currentDistance, inout int currentObject)
{
  if (distance >= EPSILON && distance < currentDistance)
  {
    currentDistance = distance;
    currentObject = i;
  }
}
vec3 refl(vec3 v, vec3 n)
{
 return v - n * 2.0 * dot(v, n);
}

vec3 raytrace(vec3 rayPosition, vec3 rayDirection)
{
  float currentDistance = INFINITY;
  int   currentObject = -1;
  int   currentGalaxy = startGalaxy;
  int   otherGalaxy = 1 - currentGalaxy;
  vec3  normal;

  float h1, h2, rayDistance;
  vec3 gravityVector, cv = rayDirection * c;
  float objectDistance;

  for (int i = 0; i < 100; i++)
  {
    currentDistance = INFINITY;
    
    // Bend the light towards the wormhole
    gravityVector = rayPosition - wormhole.xyz;
    rayDistance = length(gravityVector);
    h1 = (rayDistance - wormhole.w * c) * stepSize;
    gravityVector = (gravityVector / rayDistance) * (-gravityWormhole / (rayDistance * rayDistance));
    cv += gravityVector * h1;
    rayDirection = normalize(cv);
    cv = rayDirection * c;
    
    if (currentGalaxy == 1)
    {
      // Bend the light towards the black hole
      gravityVector = rayPosition - blackhole.xyz;
      rayDistance = length(gravityVector);
      h2 = (rayDistance - blackhole.w * c) * stepSize;
      gravityVector = (gravityVector / rayDistance) * (-gravityBlackhole / (rayDistance * rayDistance));
      cv += gravityVector * h2;
      rayDirection = normalize(cv);
      cv = rayDirection * c;
    }

    // Test against objects in this universe
    if (currentGalaxy == 0)
    {
      objectDistance = sphereDistance(rayPosition, rayDirection, saturn);
      testDistance(ID_SATURN, objectDistance, currentDistance, currentObject); 
      
      objectDistance = ringDistance(rayPosition, rayDirection, saturn.xyz, vec3(0.0, 1.0, 0.0), 1.67 * saturn.w, 2.33 * saturn.w);
      testDistance(ID_SATURN_RING, objectDistance, currentDistance, currentObject); 
    }
    else
    {
      objectDistance = sphereDistance(rayPosition, rayDirection, planet);
      testDistance(ID_PLANET, objectDistance, currentDistance, currentObject); 
      
      objectDistance = sphereDistance(rayPosition, rayDirection, blackhole);
      if (length(blackhole.xyz - rayPosition) < blackhole.w) 
      {
        objectDistance = EPSILON;
      }
      testDistance(ID_BLACKHOLE, objectDistance, currentDistance, currentObject); 
      
      objectDistance = ringDistance(rayPosition, rayDirection, blackhole.xyz, vec3(0.0, 1.0, 0.0), blackhole.w, 4.0 * blackhole.w );
      testDistance(ID_BLACKHOLE_DISK, objectDistance, currentDistance, currentObject); 
    }

    // Test against the universe itself
    objectDistance = panoramaDistance(rayPosition, rayDirection);
    if (currentGalaxy == 0)
    {
      testDistance(ID_GALAXY1, objectDistance, currentDistance, currentObject);
    }
    else
    {
      testDistance(ID_GALAXY2, objectDistance, currentDistance, currentObject);
    }

    if (currentGalaxy == 1)
    {
      rayDistance = c * min(h1, h2);
    }
    else
    {
      rayDistance = c * h1;
    }
    
    if (currentObject != -1 && currentDistance <= rayDistance * 1.1)
    {
      // Test if inside blackhole
      if (currentObject == ID_BLACKHOLE)
      {
        currentObject = -1;
      }
      break;
    }

    float d = sphereDistance(rayPosition, rayDirection, wormhole);
    if (d > 0.0 && d < rayDistance)
    { // Ray goes through wormhole
      currentGalaxy = otherGalaxy;
      otherGalaxy = 1 - currentGalaxy;
      rayPosition = 2.0 * wormhole.xyz - (rayPosition + rayDirection * rayDistance);
      gravityVector = normalize(cameraPosition - wormhole.xyz);
      rayPosition = refl(rayPosition - wormhole.xyz, gravityVector) + wormhole.xyz;
      cv = refl(-cv, gravityVector);
    }
    else
    {
      rayPosition += rayDirection * rayDistance;
    }
  }

  if (currentObject != -1)
  {
    vec3 currentPosition = rayPosition + rayDirection * currentDistance;
    vec3 viewVector = normalize(rayPosition - currentPosition);

    if (currentObject == ID_GALAXY1 || currentObject == ID_GALAXY2)
    { // Objects >= 100 are the galaxies
#if NO_EDGE
     return panoramaColor(currentGalaxy, rayDirection);
#else
     return panoramaColor(currentGalaxy, currentPosition / worldSize);
#endif
    }
    else if (currentObject == ID_BLACKHOLE_DISK)
    {
      return vec3(1.0);
    }
    else if (currentObject == ID_SATURN_RING)
    {
      objectDistance = sphereDistance(currentPosition, lightDirection, saturn);
      if (objectDistance > 0.0) 
      {
        return vec3(0.0);
      }
      return vec3(0.5);
    }
    else if (currentObject == ID_SATURN)
    {
      normal = (currentPosition - saturn.xyz) / saturn.w;

      vec3 diffuse = saturnColor(normal);
      vec3 specular = vec3(0.0);
      vec3 ambient = vec3(0.0);
      return computeShading(lightDirection, viewVector, normal, diffuse, specular, ambient);
    }
    else if (currentObject == ID_PLANET)
    {
      normal = (currentPosition - planet.xyz) / planet.w;

      vec3 diffuse = planetDiffuse;
      vec3 specular = planetSpecular;
      vec3 ambient = vec3(0.0);
      return computeShading(lightDirection, viewVector, normal, diffuse, specular, ambient);
    }
  }
  
  return vec3(0.0);
}

void main()
{
 vec3 rayDir = normalize(rayMatrix * vec4(pos.x, pos.y, 1.0, 1.0)).xyz;
 gl_FragColor = vec4(raytrace(cameraPosition, rayDir), 1.0);
}
</script>
<script type="x-shader/x-vertex" id="vertexShaderDepth">
varying vec2 pos;
void main() {
  pos = position.xy;
  gl_Position = vec4( position.xy, 0.0, 1.0 );
}
</script>

<script src="js/three.min.js"></script>
<script src="js/dat.gui.min.js"></script>

<script src="js/ConvolutionShader.js"></script>
<script src="js/CopyShader.js"></script>
<script src="js/FilmShader.js"></script>

<script src="js/EffectComposer.js"></script>
<script src="js/ShaderPass.js"></script>
<script src="js/MaskPass.js"></script>
<script src="js/RenderPass.js"></script>
<script src="js/BloomPass.js"></script>
<script src="js/FilmPass.js"></script>

<script src="js/FlyControls.js"></script>
<script src="js/Detector.js"></script>
<script src="js/stats.min.js"></script>
<script src="js/main.js"></script>

</body>
</html>
